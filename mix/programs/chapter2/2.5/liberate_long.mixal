TSIZE           EQU     0:2
SIZE            EQU     1:2
LINK            EQU     4:5
c               EQU     10
MAIN            ORIG    100
                JMP     SETUP_MEMORY
                JMP     SHOW_BLOCKS
                OUT     EMPTY(18)
                OUT     EMPTY(18)

                ENT6    MEMORY+156
                JMP     LIBERATE
                ENT6    MEMORY+401
                JMP     LIBERATE
                ENT6    MEMORY+470
                JMP     LIBERATE
                ENT6    MEMORY+430
                JMP     LIBERATE
                JMP     SHOW_BLOCKS
                HLT

SETUP_MEMORY    STJ     9F
                ENTA    MEMORY
                STA     FREE(LINK)
                ENTA    MEMORY
                STA     MEMORY(LINK)
* free blocks
                ENTA    -26
                STA     MEMORY(TSIZE) 
                STA     MEMORY+25(TSIZE) 
                ENTA    MEMORY+151
                STA     MEMORY(LINK)
                ENTA    FREE
                STA     MEMORY+1(LINK)

                ENTA    125
                STA     MEMORY+26(TSIZE)

                ENTA    -5
                STA     MEMORY+151(TSIZE)
                STA     MEMORY+155(TSIZE)
                ENTA    MEMORY+201
                STA     MEMORY+151(LINK)
                ENTA    0
                STA     MEMORY+152(LINK)

                ENTA    45
                STA     MEMORY+156(TSIZE)

                ENTA    -200
                STA     MEMORY+201(TSIZE)
                STA     MEMORY+400(TSIZE)
                ENTA    MEMORY+500
                STA     MEMORY+201(LINK)
                ENTA    MEMORY+151
                STA     MEMORY+202(LINK)

                ENTA    9
                STA     MEMORY+401(TSIZE)
                ENTA    10 
                STA     MEMORY+410(TSIZE)
                STA     MEMORY+420(TSIZE)
                STA     MEMORY+430(TSIZE)
                STA     MEMORY+440(TSIZE)
                STA     MEMORY+450(TSIZE)
                STA     MEMORY+460(TSIZE)

                ENTA    30 
                STA     MEMORY+470(TSIZE)


                ENTA    -4
                STA     MEMORY+500(TSIZE)
                STA     MEMORY+503(TSIZE)
                ENTA    MEMORY+506
                STA     MEMORY+500(LINK)
                ENTA    MEMORY+201
                STA     MEMORY+501(LINK)

                ENTA    2
                STA     MEMORY+504(TSIZE)

                ENTA    -245
                STA     MEMORY+506(TSIZE)
                STA     MEMORY+750(TSIZE)
                ENTA    ENDMEM
                STA     MEMORY+506(LINK)
                ENTA    500
                STA     MEMORY+507(LINK)
                
                ENTA    250
                STA     MEMORY+750(TSIZE)
                
9H              JMP     *

* I6 is the location of P0 (the block we are freeing)
LIBERATE        STJ     9F
                LD1     -1,6(TSIZE) * previous block tag
                LD2     0,6(SIZE)
                INC2    0,6   * next block tag
                LD2     0,2(TSIZE)
                J1N     FIRST_TAG_MINUS
FIRST_TAG_PLUS  J2N     SECOND_TAG_MINUS_ONLY
                JMP     BOTH_PLUS
FIRST_TAG_MINUS J2N     BOTH_MINUS
                JMP     FIRST_TAG_MINUS_ONLY
FIRST_TAG_MINUS_ONLY    LD3  -1,6(SIZE)
                ENT4    0,6
                DEC4    0,3 * I4 is now the previous block
                LDA     0,6(SIZE)
                ADD     0,4(SIZE)
                STA     -1,6(SIZE)
                STA     0,4(SIZE)
                JMP     9F
SECOND_TAG_MINUS_ONLY   LD3 0,6(SIZE)
                INC3    0,6 * I3 now points to the next block
                LD4     0,3(LINK) * I4 is the following block
                ST4     0,6(LINK)
                ST6     1,4(LINK)
                LD5     1,3(LINK)
                ST5     1,6(LINK)
                ST6     0,5(LINK)
                LDAN     0,6(SIZE)
                SUB     0,3(SIZE)
                LD4     0,3(SIZE)
                INC4    -1,3
                STA     0,6(TSIZE)
                STA     0,4(TSIZE)
                JMP     9F
BOTH_PLUS       LD3     0,6(SIZE)
                ENN4    0,3
                INC3    -1,6
                ST4     0,3(SIZE)  * set size at the end
                ST4     0,6(SIZE)
                LD3     FREE(LINK) * set the next link to free's link
                ST3     0,6(LINK) 
                LD4     FREE+1(LINK)
                ST4     1,6(LINK)
                ENTA    0,6
                STA     1,3(LINK) 
                STA     0,4(LINK)
                ST6     FREE   *  free now points to this node
                JMP     9F
BOTH_MINUS      ENT3    0,6  * P (previous block)
                INC3    0,1 * I1 is negative, so this goes back to previous block
                ENT4    0,3
                LDA     0,4(SIZE) * prev block size
                LD5     0,4(SIZE)
                INC4    0,5
                ADD     0,4(SIZE) * current block size
                LD5     0,4(SIZE)
                INC4    0,5
                ADD     0,4(SIZE) * next block size
                STA     0,3(SIZE) set first size
                LD5     0,3(SIZE)
                INC5    -1,3   * set last size
                STA     0,5(SIZE)
* now need to update links
                LD5     0,4(LINK)  * update links to point to each other.
                ST3     1,5(LINK)
                ST5     0,3(LINK)
           
9H              JMP     *

SHOW_BLOCKS     STJ     9F
                LDA     FREE_LBL
                STA     OUTBUF
                OUT     OUTBUF(18)
                OUT     EMPTY(18)
                LD1     FREE(LINK)
                JMP     SHOW_LIST
                
9H              JMP     *

SHOW_LIST       STJ     9F
1H              CMP1    =ENDMEM=
                JE      9F

                ENTA    0,1
                DECA    MEMORY
                CHAR
                STX     OUTBUF

                LDA     HYPHEN
                STA     OUTBUF+1

                ENTA    0,1
                ADD     0,1(SIZE)
                DECA    1
                DECA    MEMORY
                CHAR
                STX     OUTBUF+2

                OUT     OUTBUF(18)
                STZ     OUTBUF+1
                STZ     OUTBUF+2

                LD1     0,1(LINK)
                JMP     1B
9H              JMP     *

* Intended Blocks: 
* 0-25: FREE
* 26-150: In Use
* 151-155: Free
* 156-200: In Use
* 201-400: Free
* 401-499: In Use
* 500-503: Free
* 504-505: In Use
* 506-750: Free
* 751: In use
AVAIL           CON     0
FREE            CON     MEMORY
                CON     0

FREE_LBL        ALF FREE
USED_LBL        ALF USED
RSRV_LBL        ALF RESER
                ALF VED
AT_LBL          ALF   AT
HYPHEN          ALF    -

OUTBUF          ORIG *+24
EMPTY           ORIG *+24
MEMORY          ORIG    *+1000
                CON     0
ENDMEM          CON     3999

            END     MAIN