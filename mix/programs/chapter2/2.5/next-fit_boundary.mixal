TSIZE           EQU     0:2
SIZE            EQU     1:2
LINK            EQU     4:5
c               EQU     10
MAIN            ORIG    100
                JMP     SETUP_MEMORY
                JMP     SHOW_BLOCKS
                OUT     EMPTY(18)
                OUT     EMPTY(18)

                ENT5    FREE
                ENT6    10
                JMP     RESERVE
                ENT6    200
                JMP     RESERVE
                ENT6    10 
                JMP     RESERVE
                ENT6    10 
                JMP     RESERVE
                ENT6    30 
                JMP     RESERVE
                ENT6    300
                JMP     RESERVE
                ENT6    195
                JMP     RESERVE
                ENT6    15 
                JMP     RESERVE

                JMP     SHOW_BLOCKS
                HLT

SETUP_MEMORY    STJ     9F
                ENTA    MEMORY
                STA     FREE(LINK)
                ENTA    MEMORY
                STA     MEMORY(LINK)
* free blocks
                ENTA    -26
                STA     MEMORY(TSIZE) 
                STA     MEMORY+25(TSIZE) 
                ENTA    MEMORY+151
                STA     MEMORY(LINK)
                ENTA    FREE
                STA     MEMORY+1(LINK)

                ENTA    125
                STA     MEMORY+26(TSIZE)

                ENTA    -5
                STA     MEMORY+151(TSIZE)
                STA     MEMORY+155(TSIZE)
                ENTA    MEMORY+201
                STA     MEMORY+151(LINK)
                ENTA    0
                STA     MEMORY+152(LINK)

                ENTA    45
                STA     MEMORY+156(TSIZE)

                ENTA    -200
                STA     MEMORY+201(TSIZE)
                STA     MEMORY+400(TSIZE)
                ENTA    MEMORY+500
                STA     MEMORY+201(LINK)
                ENTA    MEMORY+151
                STA     MEMORY+202(LINK)

                ENTA    99
                STA     MEMORY+401(TSIZE)

                ENTA    -4
                STA     MEMORY+500(TSIZE)
                STA     MEMORY+503(TSIZE)
                ENTA    MEMORY+506
                STA     MEMORY+500(LINK)
                ENTA    MEMORY+201
                STA     MEMORY+501(LINK)

                ENTA    2
                STA     MEMORY+504(TSIZE)

                ENTA    245
                STA     MEMORY+506(TSIZE)
                STA     MEMORY+750(TSIZE)
                ENTA    ENDMEM
                STA     MEMORY+506(LINK)
                ENTA    500
                STA     MEMORY+507(LINK)
                
                ENTA    250
                STA     MEMORY+750(TSIZE)
                
9H              JMP     *

RESERVE         STJ     9F
A1              ENT1    0,5
A2              LD2     0,1(LINK) * P 
                CMP2    =ENDMEM=
                JE      A2A
                CMP5    0,1(LINK)
                JE      8F  *  went around the list
                JMP     A3
A2A             ENT1    FREE
                JMP     A2
A3              CMP6    0,2(SIZE)
                JLE     A4
                ENT1    0,2
                JMP     A2
A4              LD3     0,2(SIZE)
                DEC3    0,6 * SIZE(P) - N  * K
                
                LDA     RSRV_LBL                
                STA     OUTBUF
                LDA     RSRV_LBL+1
                STA     OUTBUF+1

                ENTA    0,6
                CHAR
                STX    OUTBUF+2

                LDA    AT_LBL
                STA    OUTBUF+3

                ENTA    0,2
                INCA    0,3
                DECA    MEMORY
                CHAR
                STX     OUTBUF+4

                OUT     OUTBUF(18)
                STZ     OUTBUF
                STZ     OUTBUF+1
                STZ     OUTBUF+2
                STZ     OUTBUF+3
                STZ     OUTBUF+4

                CMP3    =c=
                JL      A4A
                ENT4    0,2
                INC4    0,3
                DEC4    1
                ENN3    0,3
                ST3     0,2(TSIZE)
                ST3     0,4(TSIZE)
                INC4    1
                ST6     0,4(SIZE) * store the positive of the size
                INC4    0,6
                DEC4    1
                ST6     0,4
                
                JMP     8F
A4A             LD3     0,2(LINK)
                ST3     0,1(LINK)
8H              ENT5    0,1
9H              JMP     *

SHOW_BLOCKS     STJ     9F
                LDA     FREE_LBL
                STA     OUTBUF
                OUT     OUTBUF(18)
                OUT     EMPTY(18)
                LD1     FREE(LINK)
                JMP     SHOW_LIST
                
9H              JMP     *

SHOW_LIST       STJ     9F
1H              CMP1    =ENDMEM=
                JE      9F

                ENTA    0,1
                DECA    MEMORY
                CHAR
                STX     OUTBUF

                LDA     HYPHEN
                STA     OUTBUF+1

                ENTA    0,1
                ADD     0,1(SIZE)
                DECA    1
                DECA    MEMORY
                CHAR
                STX     OUTBUF+2

                OUT     OUTBUF(18)
                STZ     OUTBUF+1
                STZ     OUTBUF+2

                LD1     0,1(LINK)
                JMP     1B
9H              JMP     *

* Intended Blocks: 
* 0-25: FREE
* 26-150: In Use
* 151-155: Free
* 156-200: In Use
* 201-400: Free
* 401-499: In Use
* 500-503: Free
* 504-505: In Use
* 506-750: Free
* 751: In use
AVAIL           CON     0
FREE            CON     MEMORY
                CON     0

FREE_LBL        ALF FREE
USED_LBL        ALF USED
RSRV_LBL        ALF RESER
                ALF VED
AT_LBL          ALF   AT
HYPHEN          ALF    -

OUTBUF          ORIG *+24
EMPTY           ORIG *+24

MEMORY          ORIG    *+1000
ENDMEM          CON     3999

            END     MAIN