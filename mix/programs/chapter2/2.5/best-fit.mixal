SIZE            EQU     4:5
LINK            EQU     0:2
MAIN            ORIG    100
                JMP     SETUP_MEMORY
                JMP     SHOW_BLOCKS
                OUT     EMPTY(18)
                OUT     EMPTY(18)

                ENT6    10
                JMP     RESERVE
                ENT6    100
                JMP     RESERVE
                ENT6    50 
                JMP     RESERVE
                ENT6    2 
                JMP     RESERVE

                JMP     SHOW_BLOCKS
                HLT

SETUP_MEMORY    STJ     9F
                ENTA    MEMORY
                STA     FREE(LINK)
                ENTA    MEMORY
                STA     MEMORY(LINK)
* free blocks
                ENTA    26
                STA     MEMORY(SIZE) 
                ENTA    MEMORY+151
                STA     MEMORY(LINK)

                ENTA    5
                STA     MEMORY+151(SIZE)
                ENTA    MEMORY+201
                STA     MEMORY+151(LINK)

                ENTA    200
                STA     MEMORY+201(SIZE)
                ENTA    MEMORY+500
                STA     MEMORY+201(LINK)

                ENTA    2
                STA     MEMORY+500(SIZE)
                ENTA    MEMORY+504
                STA     MEMORY+500(LINK)

                ENTA    247
                STA     MEMORY+504(SIZE)
                ENTA    -1
                STA     MEMORY+504(LINK)
                

* used block    
                ENTA    125
                STA     MEMORY+26(SIZE)
                ENTA    MEMORY+156
                STA     MEMORY+26(LINK)

                ENTA    45
                STA     MEMORY+156(SIZE)
                ENTA    MEMORY+401
                STA     MEMORY+156(LINK)

                ENTA    99
                STA     MEMORY+401(SIZE)
                ENTA    MEMORY+502
                STA     MEMORY+401(LINK)

                ENTA    2
                STA     MEMORY+502(SIZE)
                ENTA    MEMORY+751
                STA     MEMORY+502(LINK)

                ENTA    250
                STA     MEMORY+751(SIZE)
                ENTA    -1
                STA     MEMORY+751(LINK)

9H              JMP     *

RESERVE         STJ     9F
A1              ENT1    FREE * Q
                ENT3    -1 
                ENTX    4095
                ENT4    -1
A2              LD2     0,1(LINK) * P 
                J2N     A5 
A3              CMP6    0,2(SIZE)
                JLE     A4 * candidate
                ENT1    0,2
                JMP     A2
A4              CMPX    0,2(SIZE)
                JLE     A4A
                ENT3    0,2
                ENT4    0,1
                LDX     0,2(SIZE)
A4A             ENT1    0,2
                JMP     A2


A5              J3N     8F * nothing found

                LD5     0,3(SIZE)
                DEC5    0,6 * SIZE(P) - N

                LDA     RSRV_LBL                
                STA     OUTBUF
                LDA     RSRV_LBL+1
                STA     OUTBUF+1

                ENTA    0,6
                CHAR
                STX    OUTBUF+2

                LDA    AT_LBL
                STA    OUTBUF+3

                ENTA    0,3
                INCA    0,5
                DECA    MEMORY
                CHAR
                STX     OUTBUF+4

                OUT     OUTBUF(18)
                STZ     OUTBUF
                STZ     OUTBUF+1
                STZ     OUTBUF+2
                STZ     OUTBUF+3
                STZ     OUTBUF+4


                J5Z     A5A
                ST5     0,3(SIZE)
                JMP     9F
A5A             LD5     0,3(LINK)
                ST5     0,4(LINK)
                JMP     9F
8H              LDA     SKIP_LBL
                STA     OUTBUF
                ENTA    0,6
                CHAR
                STX     OUTBUF+1
                OUT     OUTBUF(18)
                STZ     OUTBUF+1
9H              JMP     *

SHOW_BLOCKS     STJ     9F
                LDA     FREE_LBL
                STA     OUTBUF
                OUT     OUTBUF(18)
                OUT     EMPTY(18)
                LD1     FREE
                JMP     SHOW_LIST
                
*                OUT     EMPTY(18)
*                OUT     EMPTY(18)
*                LDA     USED_LBL
*                STA     OUTBUF
*                OUT     OUTBUF(18)
*                OUT     EMPTY(18)
*                LD1     USED
*                JMP     SHOW_LIST
9H              JMP     *

SHOW_LIST       STJ     9F
1H              J1N     9F

                ENTA    0,1
                DECA    MEMORY
                CHAR
                STX     OUTBUF

                LDA     HYPHEN
                STA     OUTBUF+1

                ENTA    0,1
                ADD     0,1(SIZE)
                DECA    1
                DECA    MEMORY
                CHAR
                STX     OUTBUF+2

                OUT     OUTBUF(18)
                STZ     OUTBUF+1
                STZ     OUTBUF+2

                LD1     0,1(LINK)
                JMP     1B
9H              JMP     *

* Intended Blocks: 
* 0-25: FREE
* 26-150: In Use
* 151-155: Free
* 156-200: In Use
* 201-400: Free
* 401-499: In Use
* 500-501: Free
* 502-503: In Use
* 504-750: Free
* 751: In use
AVAIL           CON     0
FREE            CON     MEMORY
*USED            CON     MEMORY+26

FREE_LBL        ALF FREE
USED_LBL        ALF USED
HYPHEN          ALF    -
SKIP_LBL        ALF SKIP

RSRV_LBL        ALF RESER
                ALF VED
AT_LBL          ALF   AT


OUTBUF          ORIG *+24
EMPTY           ORIG *+24

MEMORY          ORIG    *+1000

            END     MAIN