* equivalence class for array overlap
NAME        EQU     2:2 
PARENT      EQU     3:3
DELTA       EQU     0:1
LBD         EQU     0:1
UBD         EQU     0:1

MAIN        ORIG    100
            JMP     SETUP_DATA
            JMP     CALC_OVERLAP
            JMP     PRINT_DATA
            HLT

SETUP_DATA  STJ     9F
            ENTA    27
            STA     X(NAME)
            ENTA    28
            STA     Y(NAME)
            ENTA    1
            STA     A(NAME)
            ENTA    29
            STA     Z(NAME)

            ENTA    10
            STA     X+8(UBD)
            STA     Y+8(UBD)

            ENTA    3
            STA     Y+4(LBD)

            ENTA    1
            STA     A+4(LBD)
            STA     A+8(UBD)

            ENTA    -2
            STA     Z+4(LBD)

* setup equivalences
            ENTA    1
            STA     DATA(1:1)
            ENTA    7
            STA     DATA(2:2)
            ENTA    2
            STA     DATA(3:3)
            ENTA    3
            STA     DATA(4:4)

            ENTA    4
            STA     DATA+1(1:1)
            ENTA    3
            STA     DATA+1(3:3)

            ENTA    2
            STA     DATA+2(1:1)
            ENTA    1
            STA     DATA+2(2:2)
            ENTA    3
            STA     DATA+2(3:3)
            ENTA    8
            STA     DATA+2(4:4)

9H          JMP     *

CALC_OVERLAP STJ    9F

            ENT6    0
            ENTA    0
1H          ENT5    -3,6
            J5Z     9F   * no equivalences left

            LD5     DATA,6(3:3)   * I5 is Q, the rhs of the equivalence
            ENTA    0

2H          LD1     TABLE,5(PARENT)
            SUB     TABLE,5(DELTA)
            J1Z     3F
            ENT5    0,1
            JMP     2B

3H          LD3     DATA,6(1:1)
4H          LD1     TABLE,3(PARENT)
            ADD     TABLE,3(DELTA)
            J1Z     5F
            ENT3    0,1
            JMP     4B

5H          ENT4    0,3
            DEC4    0,5
            J4NZ    5F   * if parents are not the same

            LDX     TABLE,5(DELTA)
            DECX    TABLE,3
            JXZ     5F

            LDA     CONFLICT 
            STA     OUTBUF
            LDA     CONFLICT+1
            STA     OUTBUF+1
            OUT     OUTBUF(18)
            HLT


* I5 and I3 are the parents, and A is delta from the parent
5H          ADD     DATA,6(2:2)
            SUB     DATA,6(4:4)

            STA     TABLE,5(DELTA)
            ST3     TABLE,5(PARENT)

            LDA     TABLE+4,5(LBD)
            ADD     TABLE,5(DELTA)
            CMPA    TABLE+4,3(LBD)
            JGE     6F
            STA     TABLE+4,3(LBD)
6H          LDA     TABLE+8,5(UBD)
            ADD     TABLE,5(DELTA)
            CMPA    TABLE+8,3(UBD)
            JLE     8F
            STA     TABLE+8,3(UBD)
8H          INC6    1
            JMP     1B
9H          JMP     *

PRINT_DATA  STJ     9F
            ENT6    1
1H          ENT5    -5,6
            J5Z     9F   * no equivalences left

            LDA     TABLE,6(NAME)
            STA     OUTBUF

            LD1     TABLE,6(PARENT)
            J1Z     2F
            LDA     TABLE,1(NAME)
            STA     OUTBUF+2

2H          LDA     TABLE,6(DELTA)
            CHAR
            STX     OUTBUF+4
            JANN    3F
            ENTA    45 
            STA     OUTBUF+4(1:1)

3H          LDA     TABLE+4,6(LBD)
            CHAR
            STX     OUTBUF+6
            JANN    4F
            ENTA    45
            STA     OUTBUF+6(1:1)

4H          LDA     TABLE+8,6(UBD)
            CHAR
            STX     OUTBUF+8
            JANN    5F
            ENTA    45
            STA     OUTBUF+8(1:1)

5H          OUT     OUTBUF(18)
            ENT1    OUTBUF
            MOVE    EMPTY(24)

            INC6    1
            JMP     1B
9H          JMP     *

*  each entry is three words apart, each word 4 word words apart  
TABLE       CON     0
X           CON     0
Y           CON     0
A           CON     0
Z           CON     0
            CON     0
            CON     0
            CON     0
            CON     0
            CON     0
            CON     0
            CON     0
            CON     0

DATA        ORIG    *+3
OUTBUF      ORIG    *+24
EMPTY       ORIG    *+24
CONFLICT    ALF CONFL
            ALF ICT

            END     MAIN
