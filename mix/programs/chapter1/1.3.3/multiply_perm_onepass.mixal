* Program B in 1.3.3 from Knuth
MAXWDS    EQU  1200
X         ORIG *+MAXWDS
T         ORIG *+MAXWDS
PERM      ORIG *+MAXWDS
ANS       EQU  PERM
OUTBUF    ORIG *+24
CARDS     EQU  16
PRINTER   EQU  18
BEGIN     IN   PERM(CARDS)
          ENT2 0
          LDA  EQUALS
* Load all cards and print them out
1H        JBUS *(CARDS)
          CMPA PERM+15,2
          JE   *+2
          IN   PERM+16,2(CARDS)
          ENT1 OUTBUF
          JBUS *(PRINTER)
          MOVE PERM,2(16)
          OUT  OUTBUF(PRINTER)
          JE   1F       * we have an equals sign
          INC2 16
          CMP2 =MAXWDS-16=
          JLE  1B
          HLT  * Too much input

1H        INC2 15
          ENT3 0
RIGHT     ENTX 0
SCAN      DEC2 1
          LDA  PERM,2
          JAZ  CYCLE
          CMPA RPREN
          JE   RIGHT
          CMPA LPREN
          JE   LEFT
          ENT4 1,3
          STA  X
2H        DEC4 1
          CMPA X,4
          JNE  2B
          J4P  FOUND
          INC3 1
          STA  X,3
          ST3  T,3
          ENT4 0,3
FOUND     LDA  T,4
          STX  T,4
          SRC  5
          JANZ SCAN
          ENT1 0,4
          JMP  SCAN
LEFT      STX  T,1
CYCLE     J2P  SCAN

OUTPUT    ENT1 ANS
          J3Z  DONE
1H        LDAN X,3
          JAP  SKIP
          CMP3 T,3
          JE   SKIP
          CMP3 T,3
          JE   SKIP
          MOVE LPREN
2H        MOVE X,3
          STA  X,3
          LD3  T,3
          LDAN X,3
          JAN  2B
          MOVE RPREN
SKIP      DEC3 1
          J3P  1B

DONE      CMP1 =ANS=
          JNE  *+2
          MOVE LPREN(2)
          MOVE =0=
          MOVE -1,1(22) * 23 words of blanks
          ENT3 0
          OUT  ANS,3(PRINTER)
          INC3 24
          LDX  ANS,3
          JXNZ *-3
          HLT

* Constants*
LPREN     ALF      (
RPREN     ALF  )
EQUALS    ALF      =
          END  BEGIN
